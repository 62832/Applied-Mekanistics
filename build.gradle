buildscript {
    repositories {
        maven {
            name = "MinecraftForge"
            url = uri("https://files.minecraftforge.net/maven/")
        }

        mavenCentral()
    }

    dependencies {
        classpath(group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true)
    }
}

plugins {
    id("com.matthewprenger.cursegradle") version "1.4.0"
    id("com.diffplug.spotless") version "6.4.1"
}

apply(plugin: "net.minecraftforge.gradle")

group = "me.ramidzkh"
version = System.getenv("APPMEK_VERSION") ?: "0.0.0"

repositories {
    maven {
        name = "Modmaven"
        url = uri("https://modmaven.dev/")

        content {
            includeGroup("appeng")
        }
    }

    maven {
        name = "Progwml6 maven"
        url = uri("https://dvs1.progwml6.com/files/maven/")

        content {
            includeGroup("mezz.jei")
        }
    }

    maven {
        name = "Local"
        url = file("libs").toURI()
    }
}

dependencies {
    minecraft("net.minecraftforge:forge:1.18.2-40.0.44")

    // We depend on many AE2 internals, such as using their basic cell drive, thus not using classifier = "api"
    implementation(fg.deobf("appeng:appliedenergistics2:11.0.0-alpha.3"))

    // We depend on Mekanism internals, such as using QIO, thus not using classifier = "api"
    compileOnly(fg.deobf("mekanism:Mekanism:1.18.2-10.1.2.homebaked:api"))
    compileOnly(fg.deobf("mekanism:Mekanism:1.18.2-10.1.2.homebaked"))
    runtimeOnly(fg.deobf("mekanism:Mekanism:1.18.2-10.1.2.homebaked:all"))

    compileOnly(fg.deobf("mezz.jei:jei-1.18.2:9.7.0.180:api"))
    runtimeOnly(fg.deobf("mezz.jei:jei-1.18.2:9.7.0.180"))
}

sourceSets {
    main {
        resources {
            srcDir("src/generated/resources")
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

////////////////////
// Forge/Minecraft
minecraft {
    mappings(channel: "official", version: "1.18.2")

    /**
     * Configures properties common to all run configurations
     */
    def commonRunProperties = {
        workingDirectory(project.file("run"))

        property("forge.logging.console.level", "debug")
        property("fml.earlyprogresswindow", "false")
        jvmArgs("--add-opens", "java.base/sun.security.util=ALL-UNNAMED")
        jvmArgs("--add-opens", "java.base/java.util.jar=ALL-UNNAMED")

        // See https://github.com/Vazkii/Patchouli#mixin-troubleshooting
        property("mixin.env.remapRefMap", "true")
        property("mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg")
        // property "mixin.debug.export", "true"

        mods {
            appmek {
                source(sourceSets.main)
            }
        }
    }

    runs {
        client {
            with(commonRunProperties)
        }

        server {
            with(commonRunProperties)
        }

        data {
            with(commonRunProperties)
            // ForgeGradle will just force-exit the Gradle Daemon which fails our builds in case
            // a daemon is used for any reason.
            forceExit false
            args("--mod", "appmek", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources"))
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release.set(17)
}

processResources {
    inputs.property("version", project.version)

    exclude(".cache")

    filesMatching("META-INF/mods.toml") {
        expand("version": project.version)
    }
}

jar {
    finalizedBy("reobfJar")

    from(sourceSets.main.output.classesDirs)
    from(sourceSets.main.output.resourcesDir)

    manifest {
        attributes([
                "Specification-Title"     : "Applied Mekanistics",
                "Specification-Vendor"    : "ramidzkh",
                "Specification-Version"   : "${project.version}",
                "Implementation-Title"    : "${project.name}",
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "ramidzkh",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

/////////////
// Spotless
spotless {
    java {
        target("src/main/java/**/*.java")

        endWithNewline()
        indentWithSpaces()
        removeUnusedImports()
        toggleOffOn()
        eclipse().configFile("codeformat/codeformat.xml")
        importOrderFile("codeformat/ae2.importorder")
    }

    format("json") {
        target("src/*/resources/**/*.json")
        targetExclude("src/generated/resources/**")
        prettier().config(parser: "json")
    }
}

////////////////
// CurseForge
System.getenv("CURSEFORGE")?.with { String key ->
    curseforge {
        apiKey = key

        project {
            id = "574300"
            changelogType = "markdown"
            changelog = "View changelog at [the release page](https://github.com/AppliedEnergistics/Applied-Mekanistics/releases/tag/${version})"

            if (version.contains("alpha")) {
                releaseType = "alpha"
            } else if (version.contains("beta")) {
                releaseType = "beta"
            } else {
                releaseType = "release"
            }

            addGameVersion("1.18.2")
            addGameVersion("Forge")

            mainArtifact(jar) {
                displayName = "${project.version}"

                relations {
                    requiredDependency("applied-energistics-2")
                    requiredDependency("mekanism")
                }
            }
        }
    }
}
